#vs_begin
#version 460 core
layout (location = 0) in vec3 aPos;

uniform mat4 _Model;

void main()
{
    gl_Position = _Model * vec4(aPos, 1.0);
}
#vs_end

#gs_begin
#version 460 core
layout (triangles) in;
layout (triangle_strip, max_vertices=18) out;

uniform mat4 _ShadowMatrices[6];

out vec4 FragPos; // FragPos from GS (output per emitvertex)

void main()
{
    for(int face = 0; face < 6; ++face)
    {
        gl_Layer = face; // built-in variable that specifies to which face we render.
        for(int i = 0; i < 3; ++i) // for each triangle's vertices
        {
            FragPos = gl_in[i].gl_Position;
            gl_Position = _ShadowMatrices[face] * FragPos;
            EmitVertex();
        }
        EndPrimitive();
    }
} 
#gs_end

#fs_begin
#version 460 core
in vec4 FragPos;

uniform vec3 _LightPos;
uniform float _FarPlane;

void main()
{
    float lightDistance = length(FragPos.xyz - _LightPos);
    
    // map to [0;1] range by dividing by _FarPlane
    lightDistance = lightDistance / _FarPlane;
    
    // write this as modified depth
    gl_FragDepth = lightDistance;
}
#fs_end